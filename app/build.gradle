apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId 'ro.edi.xbnr'
        minSdkVersion 24 // Android 7.0
        targetSdkVersion 31 // Android 12
        versionCode 14
        versionName '1.5'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'room.schemaLocation'  : "$projectDir/schemas".toString(),
                        'room.incremental'     : 'true',
                        'room.expandProjection': 'true']
            }
        }
    }

    buildTypes {
        debug {

        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // enable support for the new language APIs
        coreLibraryDesugaringEnabled true

        // set Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    buildFeatures {
        dataBinding true
        // viewBinding true
    }

    lint {
        checkDependencies true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Java 8+ API desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.10'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0-native-mt'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0-native-mt'

    // AndroidX
    implementation 'androidx.appcompat:appcompat:1.4.1'
    // implementation 'androidx.appcompat:appcompat-resources:1.4.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // Architecture Components: LiveData, ViewModel...
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.4.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1'
    // kapt 'androidx.lifecycle:lifecycle-compiler:2.4.1'
    // implementation 'androidx.lifecycle:lifecycle-common-java8:2.4.1'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    // Room
    implementation 'androidx.room:room-runtime:2.4.1'
    kapt 'androidx.room:room-compiler:2.4.1'
    implementation 'androidx.room:room-ktx:2.4.1'
    testImplementation 'androidx.room:room-testing:2.4.1'

    // Material Design
    implementation 'com.google.android.material:material:1.5.0'

    // OkHttp
    implementation platform('com.squareup.okhttp3:okhttp-bom:4.9.3')
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.squareup.okhttp3:logging-interceptor'
    // implementation 'com.squareup.okio:okio:2.5.0'

    // TikXML
    implementation 'com.tickaroo.tikxml:annotation:0.8.13'
    implementation 'com.tickaroo.tikxml:core:0.8.13'
    kapt 'com.tickaroo.tikxml:processor:0.8.13'
    implementation 'com.tickaroo.tikxml:retrofit-converter:0.8.13'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Coil
    implementation 'io.coil-kt:coil:1.4.0'

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // testing dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.4.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    testImplementation 'junit:junit:4.13.2'
}
